>>SOURCE FORMAT FREE
*> # Importations
IDENTIFICATION DIVISION.
PROGRAM-ID. MinMax.

*> # Données
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
REPOSITORY. 
    FUNCTION RANDOM INTRINSIC.

INPUT-OUTPUT SECTION.
FILE-CONTROL.
	SELECT OPTIONS-FILE ASSIGN "data/minmax_options.txt"
		ORGANIZATION IS LINE SEQUENTIAL
		STATUS IS FILE-STATUS.

DATA DIVISION.
FILE SECTION.
FD	OPTIONS-FILE.
01	OPTIONS-LINE			PIC X(80).

WORKING-STORAGE SECTION.
01	FILE-STATUS				PIC XX.
01	WS-MINIMUM					PIC 9(5) VALUE 1.
01	WS-MAXIMUM					PIC 9(5) VALUE 100.
01	WS-TOURS					PIC 9(3) VALUE 5.
01	WS-VALEUR					PIC 9(5).
01	WS-CIBLE					PIC 9(5).
01	WS-ITERATOR				PIC 9(3).
01	WS-MENU-CHOICE			PIC 9 VALUE 0.
01	WS-OPTION-CHOICE		PIC 9 VALUE 0.
01	WS-VALID					PIC X VALUE "N".
01	WS-PROMPT-MESSAGE		PIC X(40).
01	WS-PROMPT-MIN			PIC 9(5).
01	WS-PROMPT-MAX			PIC 9(5).
01	WS-PROMPT-RETOUR		PIC 9(5).
01	WS-ENTREE					PIC 9(5).
*> Variables d'affichage sans zéros de tête
01	DISP-MIN					PIC Z(4)9.
01	DISP-MAX					PIC Z(4)9.
01	DISP-TOURS				PIC Z(2)9.
01	DISP-CIBLE				PIC Z(4)9.
01	DISP-ITERATOR			PIC Z(2)9.
01	DISP-PROMPT-MIN			PIC Z(4)9.
01	DISP-PROMPT-MAX			PIC Z(4)9.

*> # Fonctions principales & utilitaires
PROCEDURE DIVISION.
*> ## Main
MAIN-PARAGRAPH.
	PERFORM CHARGER-OPTIONS
	PERFORM UNTIL WS-MENU-CHOICE = 3
		PERFORM AFFICHER-MENU-PRINCIPAL
		MOVE "? = " TO WS-PROMPT-MESSAGE
		MOVE 1 TO WS-PROMPT-MIN
		MOVE 3 TO WS-PROMPT-MAX
		PERFORM LIRE-ENTIER
		MOVE WS-PROMPT-RETOUR TO WS-MENU-CHOICE
		EVALUATE WS-MENU-CHOICE
			WHEN 1	PERFORM JOUER-PARTIE
			WHEN 2	PERFORM GERER-OPTIONS
		END-EVALUATE
	END-PERFORM
	STOP RUN.

*> ## Lecture / écriture des options (fichier)
CHARGER-OPTIONS.
	OPEN INPUT OPTIONS-FILE
	IF FILE-STATUS = "00"
		READ OPTIONS-FILE
		UNSTRING OPTIONS-LINE DELIMITED BY ","
			INTO WS-MINIMUM WS-MAXIMUM WS-TOURS
	END-IF
	CLOSE OPTIONS-FILE.

SAUVEGARDER-OPTIONS.
	MOVE WS-MINIMUM TO DISP-MIN
	MOVE WS-MAXIMUM TO DISP-MAX
	MOVE WS-TOURS TO DISP-TOURS
	OPEN OUTPUT OPTIONS-FILE
	STRING FUNCTION TRIM(DISP-MIN) "," FUNCTION TRIM(DISP-MAX) "," FUNCTION TRIM(DISP-TOURS)
		INTO OPTIONS-LINE
	WRITE OPTIONS-LINE
	CLOSE OPTIONS-FILE.
	
*> ## Menus
AFFICHER-MENU-PRINCIPAL.
	DISPLAY SPACE
	DISPLAY "#### Menu ####"
	DISPLAY "1 : Jouer"
	DISPLAY "2 : Options"
	DISPLAY "3 : Quitter".

AFFICHER-MENU-OPTIONS.
	MOVE WS-MINIMUM TO DISP-MIN
	MOVE WS-MAXIMUM TO DISP-MAX
	MOVE WS-TOURS TO DISP-TOURS
	DISPLAY SPACE
	DISPLAY "#### Menu options ####"
	DISPLAY "1 : Choisir les limites (actuellement : " 
		FUNCTION TRIM(DISP-MIN) " - " FUNCTION TRIM(DISP-MAX) ")"
	DISPLAY "2 : Nombre de tours max (actuellement : " 
		FUNCTION TRIM(DISP-TOURS) ")".

*> ## Lecture sécurisée d'un entier
LIRE-ENTIER.
	MOVE "N" TO WS-VALID
	PERFORM UNTIL WS-VALID = "Y"
		DISPLAY FUNCTION TRIM(WS-PROMPT-MESSAGE) " " WITH NO ADVANCING
		ACCEPT WS-ENTREE
		IF WS-ENTREE >= WS-PROMPT-MIN AND WS-ENTREE <= WS-PROMPT-MAX
			MOVE WS-ENTREE TO WS-PROMPT-RETOUR
			MOVE "Y" TO WS-VALID
		ELSE
			MOVE WS-PROMPT-MIN TO DISP-PROMPT-MIN
			MOVE WS-PROMPT-MAX TO DISP-PROMPT-MAX
			DISPLAY "Veuillez entrer un entier entre "
					FUNCTION TRIM(DISP-PROMPT-MIN) " et " 
					FUNCTION TRIM(DISP-PROMPT-MAX) "."
		END-IF
	END-PERFORM.

*> ## Partie de jeu
JOUER-PARTIE.
	COMPUTE WS-CIBLE = WS-MINIMUM +
		FUNCTION INTEGER(FUNCTION RANDOM *
		(WS-MAXIMUM - WS-MINIMUM + 1))
	MOVE WS-MINIMUM TO DISP-MIN
	MOVE WS-MAXIMUM TO DISP-MAX
	MOVE WS-TOURS TO DISP-TOURS
	DISPLAY SPACE
	DISPLAY "Trouvez entre " FUNCTION TRIM(DISP-MIN) " et " FUNCTION TRIM(DISP-MAX)
			" en " FUNCTION TRIM(DISP-TOURS) " tours"
	PERFORM JOUE-BOUCLE VARYING WS-ITERATOR FROM 1 BY 1
		UNTIL WS-ITERATOR > WS-TOURS
	IF WS-VALEUR NOT = WS-CIBLE
		MOVE WS-CIBLE TO DISP-CIBLE
		DISPLAY SPACE
		DISPLAY "Perdu ! La réppnse était " FUNCTION TRIM(DISP-CIBLE) "."
	END-IF.

JOUE-BOUCLE.
	MOVE WS-ITERATOR TO DISP-ITERATOR
	MOVE "Tour " TO WS-PROMPT-MESSAGE
	STRING "Tour " FUNCTION TRIM(DISP-ITERATOR) ":" DELIMITED BY SIZE
		INTO WS-PROMPT-MESSAGE
	MOVE WS-MINIMUM TO WS-PROMPT-MIN
	MOVE WS-MAXIMUM TO WS-PROMPT-MAX
	PERFORM LIRE-ENTIER
	MOVE WS-PROMPT-RETOUR TO WS-VALEUR
	PERFORM JOUE-EVALUATION.

JOUE-EVALUATION.
	IF WS-VALEUR = WS-CIBLE
		DISPLAY SPACE
		DISPLAY "Gagné !"
		MOVE WS-TOURS TO WS-ITERATOR
	ELSE
		IF WS-VALEUR > WS-CIBLE
			DISPLAY "-"
		ELSE
			DISPLAY "+"
		END-IF
	END-IF.

*> ## Gestion du menu Options
GERER-OPTIONS.
	PERFORM AFFICHER-MENU-OPTIONS
	MOVE "? = " TO WS-PROMPT-MESSAGE
	MOVE 1 TO WS-PROMPT-MIN
	MOVE 2 TO WS-PROMPT-MAX
	PERFORM LIRE-ENTIER
	MOVE WS-PROMPT-RETOUR TO WS-OPTION-CHOICE
	EVALUATE WS-OPTION-CHOICE
		WHEN 1	PERFORM AJUSTER-LIMITES
		WHEN 2	PERFORM AJUSTER-TOURS
	END-EVALUATE
	PERFORM SAUVEGARDER-OPTIONS.

AJUSTER-LIMITES.
	MOVE "Min = " TO WS-PROMPT-MESSAGE
	MOVE 1 TO WS-PROMPT-MIN
	MOVE WS-MAXIMUM TO WS-PROMPT-MAX
	PERFORM LIRE-ENTIER
	MOVE WS-PROMPT-RETOUR TO WS-MINIMUM
	MOVE "Max = " TO WS-PROMPT-MESSAGE
	MOVE WS-MINIMUM TO WS-PROMPT-MIN
	MOVE 10000 TO WS-PROMPT-MAX
	PERFORM LIRE-ENTIER
	MOVE WS-PROMPT-RETOUR TO WS-MAXIMUM.

AJUSTER-TOURS.
	MOVE "Tours max = " TO WS-PROMPT-MESSAGE
	MOVE 1 TO WS-PROMPT-MIN
	MOVE 100 TO WS-PROMPT-MAX
	PERFORM LIRE-ENTIER
	MOVE WS-PROMPT-RETOUR TO WS-TOURS.